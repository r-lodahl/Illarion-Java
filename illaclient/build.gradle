/*
 * This file is part of the Illarion project.
 *
 * Copyright Â© 2016 - Illarion e.V.
 *
 * Illarion is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Illarion is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 */
apply plugin: 'application'
apply plugin: 'maven-publish'

apply from: '../versions.gradle'

archivesBaseName = 'illarion_client'
mainClassName = 'illarion.client.IllaClient'

dependencies {
    implementation project(':common')
    implementation project(':engine')
    implementation project(':engine-libgdx')
    implementation project(':engine-nifty')
    implementation project(':nifty-controls')
    runtimeOnly project(':nifty-style')
    implementation group: 'com.github.nifty-gui', name: 'nifty', version: project.ext.niftyGuiVersion
    implementation group: 'com.github.nifty-gui', name: 'nifty-default-controls', version: project.ext.niftyGuiVersion
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    implementation group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'
    implementation group: 'com.github.nifty-gui', name: 'nifty', version: project.ext.niftyGuiVersion
    runtimeOnly group: 'org.illarion', name: 'rsc_books', version: project.ext.illarionResourcesVersion
    runtimeOnly group: 'org.illarion', name: 'rsc_chars', version: project.ext.illarionResourcesVersion
    runtimeOnly group: 'org.illarion', name: 'rsc_effects', version: project.ext.illarionResourcesVersion
    runtimeOnly group: 'org.illarion', name: 'rsc_gui', version: project.ext.illarionResourcesVersion
    runtimeOnly group: 'org.illarion', name: 'rsc_items', version: project.ext.illarionResourcesVersion
    runtimeOnly group: 'org.illarion', name: 'rsc_music', version: project.ext.illarionResourcesVersion
    runtimeOnly group: 'org.illarion', name: 'rsc_sounds', version: project.ext.illarionResourcesVersion
    runtimeOnly group: 'org.illarion', name: 'rsc_tables', version: project.ext.illarionResourcesVersion
    runtimeOnly group: 'org.illarion', name: 'rsc_tiles', version: project.ext.illarionResourcesVersion
}

jar {
    dependsOn { gitVersion }
    manifest {
        attributes(
                'Main-Class': 'illarion.client.IllaClient',
                'Permissions': 'all-permissions',
                'Codebase': 'illarion.org',
                'Application-Name': 'Illarion Client',
                'Application-Library-Allowable-Codebase': 'illarion.org',
                'Built-Date': new Date().format('yyyy-MM-dd HH:mm:ssZ'),
                'Built-By': System.getProperty('user.name'),
                'Built-JDK': System.getProperty('java.version'),
                'Implementation-Title': 'Illarion Client',
                'Implementation-Vendor': 'Illarion e.V.',
                'Implementation-Version': project.ext.fullVersion,
                'Source-Branch': project.ext.branch,
        )
    }
}

run {
    setIgnoreExitValue(true)

    if (org.gradle.internal.os.OperatingSystem.current() == org.gradle.internal.os.OperatingSystem.MAC_OS) {
        // Required to run LWJGL3 Java apps on MacOS
        jvmArgs += "-XstartOnFirstThread"
        jvmArgs += "-Djava.awt.headless=true"
    }

    jvmArgs += '-Dillarion.server=devserver'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url project.ext.mavenRepo
        }
    }
}
